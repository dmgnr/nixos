{
  description = "Thorium using Nix Flake";

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";

  outputs = {
    self,
    nixpkgs,
    ...
  }: {
    packages.x86_64-linux = {
      thorium = let
        pkgs = import nixpkgs {system = "x86_64-linux";};
        name = "thorium";
        version = "130.0.6723.174 - 62";
        src = pkgs.fetchurl {
          url = "https://github.com/Alex313031/thorium/releases/download/M130.0.6723.174/thorium_browser_130.0.6723.174_AVX2.AppImage";
          sha256 = "sha256-Ej7OIdAjYRmaDlv56ANU5pscuwcBEBee6VPZA3FdxsQ=";
        };
        appimageContents = pkgs.appimageTools.extractType2 {inherit name src;};
      in
        pkgs.appimageTools.wrapType2 {
          inherit name version src;
          extraInstallCommands = ''
            install -m 444 -D ${appimageContents}/thorium-browser.desktop $out/share/applications/thorium-browser.desktop
            install -m 444 -D ${appimageContents}/thorium.png $out/share/icons/hicolor/512x512/apps/thorium.png
            substituteInPlace $out/share/applications/thorium-browser.desktop \
            --replace 'Exec=AppRun --no-sandbox %U' 'Exec=${name} %U'
          '';
        };
      # AVX is compatible with most CPUs
      default = self.packages.x86_64-linux.thorium;
    };

    apps.x86_64-linux = {
      thorium = {
        type = "app";
        program = "${self.packages.x86_64-linux.thorium}/bin/thorium";
      };

      default = self.apps.x86_64-linux.thorium;
    };
  };
}
